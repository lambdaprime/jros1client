// include xfunction for updateVersion
buildscript {
  repositories {
    mavenCentral()
  }
  dependencies {
    classpath "io.github.lambdaprime:id.xfunction:14.0"
  }
}

plugins {
  id 'com.diffplug.spotless'
  id 'java-library'
}

apply plugin : 'eclipse'

eclipse.classpath.file {
  // instruct eclipse plugin to put everything to module path
  whenMerged {
    entries.findAll { it.kind == 'lib' }.each { it.entryAttributes['module'] = 'true' }
  }
}

// Excluding transient dependencies which are not used
configurations {
  implementation {
    exclude(group: 'xml-apis', module: 'xml-apis')
    exclude(group: 'junit', module: 'junit')
    exclude(group: 'log4j', module: 'log4j')
    exclude(group: 'javax.servlet', module: 'servlet-api')
  }
  apiElements {
    exclude(group: 'xml-apis', module: 'xml-apis')
    exclude(group: 'junit', module: 'junit')
    exclude(group: 'log4j', module: 'log4j')
    exclude(group: 'javax.servlet', module: 'servlet-api')
  }
}

dependencies {
  api "io.github.lambdaprime:jrosclient:6.0"
  api "io.github.lambdaprime:jros1messages:2.0"
  api "io.github.lambdaprime:ICE:2.0"

  def xmlrpcVersion = "3.1.3"
  api "org.apache.xmlrpc:xmlrpc-client:${xmlrpcVersion}"
  api "org.apache.xmlrpc:xmlrpc-server:${xmlrpcVersion}"
}

jar {
  manifest { 
    attributes 'Implementation-Version': "${rootProject.version}"
  }
}

java {
  // disable Gradle modularity support since it is not working properly
  // for non modules like xmlrpc and setup module path manually later:
  // https://github.com/gradle/gradle/issues/17609
  // https://github.com/gradle/gradle/issues/12630
  modularity.inferModulePath = false
}

compileJava {
  doFirst {
    options.compilerArgs = [
      '--module-path', classpath.asPath
    ]
    classpath = files()
  }
}

javadoc {
  options.modulePath = classpath.getFiles().stream()
    .collect(java.util.stream.Collectors.toList())
  options.noTimestamp = true
  options.noQualifiers = ["all"]
  options.noTree = true
  options.noIndex = true
  options.noNavBar = true
  // ignore missing @return, @param etc
  options.addBooleanOption('Xdoclint:all,-missing', true)
  // to support @inheritDoc we need to provide sources for inherited
  // classes as part of '--module-source-path' option
  // we use "Module-pattern form" for that. It replaces * with
  // matched folder name which should also math module name itself
  // https://docs.oracle.com/en/java/javase/18/docs/specs/man/javac.html#the-module-source-path-option
  options.addStringOption('-module-source-path',
   "${project.rootDir}/*/src/main/java:" +
   "${project.rootDir}/../jrosclient/*/src/main/java")
  exclude 'id/jros1client/app/**'

  // Gradle javadoc considers all from java sourceSet only
  // This means that doc-files/* are excluded which is not
  // very compliant to javadoc https://docs.oracle.com/en/java/javase/13/javadoc/source-files.html
  // to fix that we copy doc-files/* manually
  doLast {
    copy {
      from "$projectDir/src/main/java"
      into "$project.docsDir/javadoc/${projectName}"
      include "**/doc-files/*"
    }
  }
}

def packageDir = "$buildDir/package/${projectName}"

task createRelease(type: Zip) {
  archiveFileName = "$releaseFileName"
  destinationDirectory = file("$projectDir/release")
  from file("$packageDir/..")
}

task copyLibs(type: Copy) {
  from configurations.runtimeClasspath
  from jar.archiveFile
  into "${packageDir}/libs"
}

task copySamples(type: Copy) {
  from("${project.rootDir}/jros1client.samples/src") {
    include "**"
  }
  into "${packageDir}/samples"
}

task createPackage(type: Copy, dependsOn: [copyLibs, copySamples]) {
  from "${project.rootDir}/README.md"
  from "${project.rootDir}/LICENSE-2.0.txt"
  from file("$projectDir/src/jros1client")
  into packageDir
}

task cleanRelease(type: Delete) {
  delete "$packageDir"
  delete "$projectDir/release/*-SNAPSHOT.zip"
}

createRelease.dependsOn createPackage, javadoc
assemble.dependsOn createRelease
clean.dependsOn cleanRelease

// run spotless and format code before the build
classes.dependsOn spotlessApply

task updateVersion() {
  // update version only when it is specified and non SNAPSHOT version is built
  if (version || version.contains("SNAPSHOT")) return;
  println "Updating version to " + version
  new id.xfunction.text.Substitutor()
    .withRegexpSupport()
    .substitute(java.nio.file.Paths.get("${project.rootDir}/README.md"), Map.of(
      "io.github.lambdaprime:jros1client:[0-9.]+", "io.github.lambdaprime:jros1client:" + version
    ));
}
createPackage.dependsOn updateVersion

if (project.hasProperty('publish')) {

  // Initially it was taken from https://central.sonatype.org/publish/publish-gradle/
  // but since Gradle removed this plugin https://docs.gradle.org/7.0/userguide/upgrading_version_6.html#removal_of_the_legacy_maven_plugin
  // it was rewritten for new one

  task javadocJar(type: Jar) {
    archiveClassifier = 'javadoc'
    from "README-javadoc.txt"
  }

  task sourcesJar(type: Jar) {
    archiveClassifier = 'sources'
    from "README-sources.txt"
  }

  java {
    withJavadocJar()
    withSourcesJar()
  }

  apply plugin: 'maven-publish'

  publishing {
    publications {
      mavenJava(MavenPublication) {
        artifactId = 'jros1client'
        groupId = 'io.github.lambdaprime'
        from components.java
        pom {
          name = 'jros1client'
          description = 'Java module to interact with ROS1 (Robot Operating System)'
          url = 'http://portal2.atwebpages.com/jrosclient'

          licenses {
            license {
              name = 'The Apache License, Version 2.0'
              url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
            }
          }

          developers {
            developer {
              id = 'lambdaprime'
              email = 'intid@protonmail.com'
            }
          }

          scm {
            url = 'https://github.com/lambdaprime/jros1client'
          }
        }
      }
    }
    repositories {
      maven {
        credentials {
          username ossrhUsername
          password ossrhPassword
        }
        url = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
      }
    }    
  }

  if (project.hasProperty('signing.secretKeyRingFile')) {
    apply plugin: 'signing'

    signing {
      sign publishing.publications.mavenJava
    }
  }
}
